100 SCREEN2:ONINTERVAL=186GOSUB3000:CLS:COLOR15,1,1:GOSUB180:X=1:Y=1:N=1:INTERVALON:GOTO170
110 '--- KEY ROUTINE ---
120 S=STICK(0):IFS=0THEN120
130 IFS=1THENIFM(X+X(N,1,1),Y+Y(N,1,1))=0THENX=X-(N=2)+(N=4):Y=Y+(N=1)-(N=3)ELSEGOTO120
140 IFS=3THENN=N-(N<>4)+3*(N=4)
150 IFS=5THENN=N+2*(N>2)-2*(N<3)
160 IFS=7THENN=N+(N<>1)-3*(N=1)
170 GOSUB350:GOTO120
180 '--- DISPLAY DATA MAKING ---
190 DIM X(4,2,4),Y(4,2,4),M(7,7)
200 FORI=0TO2:FORJ=0TO4:X(1,I,J)=I-1:Y(1,I,J)=-J :NEXTJ,I
210 FORI=0TO2:FORJ=0TO4:X(2,I,J)=J  :Y(2,I,J)=I-1:NEXTJ,I
220 FORI=0TO2:FORJ=0TO4:X(3,I,J)=1-I:Y(3,I,J)=J  :NEXTJ,I
230 FORI=0TO2:FORJ=0TO4:X(4,I,J)=-J :Y(4,I,J)=1-I:NEXTJ,I
240 '--- MAZE DATA READING ---
250 FORY=0TO7:FORX=0TO7:READM(X,Y):NEXTX,Y:RETURN
260 DATA 1,1,1,1,1,1,1,1
270 DATA 1,0,0,0,0,0,0,1
280 DATA 1,0,1,0,1,0,1,1
290 DATA 1,0,1,0,0,0,0,1
300 DATA 1,0,1,0,1,0,0,1
310 DATA 1,0,0,0,1,0,0,1
320 DATA 1,0,1,0,0,0,0,1
330 DATA 1,1,1,1,1,1,1,1
340 '--- DISPLAY MAKING ---
350 CLS:LINE(10,10)-(130,130),,B
360 IFM(X+X(N,0,0),Y+Y(N,0,0))=1THENDRAW"BM10,10F20D80G20":P0=20ELSEP0=0:IFM(X+X(N,0,1),Y+Y(N,0,1))=1THENDRAW"BM10,30R20D80L20"
370 IFM(X+X(N,2,0),Y+Y(N,2,0))=1THENDRAW"BM130,10G20D80F20":R0=20ELSER0=0:IFM(X+X(N,2,1),Y+Y(N,2,1))=1THENDRAW"BM130,30L20D80R20"
380 IFM(X+X(N,1,1),Y+Y(N,1,1))=1THENLINE(30,30)-(110,110),,B:RETURN
390 IFM(X+X(N,0,1),Y+Y(N,0,1))=1THENDRAW"BM30,30F20D40G20":P1=10ELSEP1=0:IFM(X+X(N,0,2),Y+Y(N,0,2))=1THENLINE(10+P0,50)-(50,90),,B
400 IFM(X+X(N,2,1),Y+Y(N,2,1))=1THENDRAW"BM110,30G20D40F20":R1=10ELSER1=0:IFM(X+X(N,2,2),Y+Y(N,2,2))=1THENLINE(90,50)-(130-R0,90),,B
410 IFM(X+X(N,1,2),Y+Y(N,1,2))=1THENLINE(50,50)-(90,90),,B:RETURN
420 IFM(X+X(N,0,2),Y+Y(N,0,2))=1THENDRAW"BM50,50F10D20G10":P2=5ELSEP2=0:IFM(X+X(N,0,3),Y+Y(N,0,3))=1THENLINE(40+P1,60)-(60,80),,B
430 IFM(X+X(N,2,2),Y+Y(N,2,2))=1THENDRAW"BM90,50G10D20F10":R2=5ELSER2=0:IFM(X+X(N,2,3),Y+Y(N,2,3))=1THENLINE(80,60)-(100-R1,80),,B
440 IFM(X+X(N,1,3),Y+Y(N,1,3))=1THENLINE(60,60)-(80,80),,B:RETURN
450 IFM(X+X(N,0,3),Y+Y(N,0,3))=1THENDRAW"BM60,60F5D10G5"ELSEIFM(X+X(N,0,4),Y+Y(N,0,4))=1THENLINE(55+P2,65)-(65,75),,B
460 IFM(X+X(N,2,3),Y+Y(N,2,3))=1THENDRAW"BM80,60G5D10F5"ELSEIFM(X+X(N,2,4),Y+Y(N,2,4))=1THENLINE(75,65)-(85-R2,75),,B
470 IFM(X+X(N,1,4),Y+Y(N,1,4))=1THENLINE(65,65)-(75,75),,B
480 RETURN
3000 PLAY"v0m1000s14o4l4erl64drcrcrl8dr16":RETURN
